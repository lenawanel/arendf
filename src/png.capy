core :: mod "core";
util :: import "util.capy";
zlib :: import "zlib.capy";
vec :: import "vec.capy";
Vec :: vec.Vec;

check_magic :: (buf: [] u8) -> bool {
    magic_nr :: [8] u8 { 0x89, 0x50, 0x4e, 0x47, 0x0d, 0x0a, 0x1a, 0x0a };
    idx : usize = 0;
    magic : bool = true;
    while idx < 8 {
        magic = magic && { buf[idx] == magic_nr[idx] };
        idx = idx + 1;
    }
    magic
}

decode :: (buf: [] u8) -> vec.RGBbuf {
    util.assert(check_magic(buf), "file must start with the magic number");
    iter := ChunksIter {
        png_data: buf,
        curr_chunk: 8,
    };
    ihdr :: parse_ihdr(^iter);
    core.println(ihdr);

    idat := vec.new();
    defer vec.dealloc(idat);

    while next_section(^mut iter) {
        if at_idat(^iter) {
            data :: curr_data(^iter);
            vec.append(^mut idat, data);
        }
    }
    util.assert(at_iend(^iter), "last chunk must be a IEND");
    ucmp_data :: zlib.uncompress_slice(idat.buf);
    defer core.libc.free(ucmp_data.ptr);

    idx := 0;
    scan_line_length :: ihdr.width * 4 + 1;
    rgb_buf := vec.new_rgb_buf (ihdr.width, ihdr.height);

    while idx < ucmp_data.len {
        pidx := 1;
        curr_pix := [3] u8 {0, 0, 0};
        while pidx < scan_line_length {
            curr_pix = vec.add(curr_pix, 
                                [3] u8 {
                                    ucmp_data[idx + pidx],
                                    ucmp_data[idx + pidx + 1],
                                    ucmp_data[idx + pidx + 2],
                                }
                            );
            
            vec.push_rgb(^mut rgb_buf, curr_pix as [3] u8);
            pidx = pidx + 4;
        }
        util.assert(ucmp_data[idx] == 1, "filter type must be 1");
        idx = idx + scan_line_length;
    }

    rgb_buf
}

ChunksIter :: struct {
    png_data: [] u8,
    curr_chunk: usize,
};

cur_len :: (iter: ^ChunksIter) -> usize {
    len :: util.read_u32_big_endian(iter.png_data, iter.curr_chunk) as usize;
    util.assert(len + iter.curr_chunk < iter.png_data.len, "all chunks must have a valid length");
    len
}

next_section :: (iter: ^mut ChunksIter) -> bool {
    len :: cur_len(iter); 
    next_chunk :: iter.curr_chunk + 12 + len;
    
    if iter.png_data.len > next_chunk {
        iter.curr_chunk = next_chunk;
        true
    } else {
        false
    }
}

curr_type_ref :: (iter: ^ChunksIter) -> ChunkTypeRef {
    // TODO: invalid pngs can cause 8byte OOB read here
    loc : ^u8 : core.ptr.const_offset(iter.png_data.ptr, iter.curr_chunk + 4) as ^u8;
    
    ChunkTypeRef {
        loc: loc,
    }
}

curr_type :: (iter: ^ChunksIter) -> u32 {
    util.read_u32(iter.png_data, iter.curr_chunk + 4)
}

ChunkTypeRef :: struct {
    loc: ^u8,
};

print_type :: (type: ^ChunkTypeRef) {
    util.write(1, type.loc, 4);
    core.println("");
}

at_ihdr :: (iter: ^ChunksIter) -> bool {
    curr_type(iter) == 0x52444849
}

parse_ihdr :: (iter: ^ChunksIter) -> IHDR {
    util.assert(at_ihdr(iter), "file has to begin with an ihdr chunk");
    data_idx :: iter.curr_chunk + 8;
    IHDR {
        width:             util.read_u32_big_endian(iter.png_data, data_idx),
        height:            util.read_u32_big_endian(iter.png_data, data_idx + 4),
        bit_depth:         iter.png_data[data_idx + 8],
        color_type:        iter.png_data[data_idx + 9],
        compression_type:  iter.png_data[data_idx + 10],
        filter_method:     iter.png_data[data_idx + 11],
        inter_lace_methog: iter.png_data[data_idx + 12],
    }
}

IHDR :: struct {
    width:             u32,
    height:            u32,
    bit_depth:         u8,
    color_type:        u8,
    compression_type:  u8,
    filter_method:     u8,
    inter_lace_methog: u8
};

at_idat :: (iter: ^ChunksIter) -> bool {
    curr_type(iter) == 0x54414449
}

at_iend :: (iter: ^ChunksIter) -> bool {
    curr_type(iter) == 0x444e4549
}

curr_data :: (iter: ^ChunksIter) -> [] u8 {
    data : [] u8 = iter.png_data;
    data.ptr = core.ptr.const_offset(data.ptr, iter.curr_chunk + 8) as ^u8;
    data.len = cur_len(iter);
    data
}